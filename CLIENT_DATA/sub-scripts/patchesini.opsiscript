encoding=utf8


if ($Flag_winst_patches$ = "on") or ($MasterFlag$ = "on")
	Message "testing patches functions"
	comment ">>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>"
	comment ""
	comment "testing patches functions"
	comment ""
	comment ">>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>"
	Set $ExpectedResult$ = "o.k."
	; start testing
	;Set $HomeTestFiles$ = "C:\tmp\testFiles"
	set $TestResult$ = "o.k."
	
	; Chapter 7.2 Patches Sectione
	
	comment ""
	comment "-------------------------------------"
	comment "Testing: "
	comment "add, set, addnew, change, del, replace"
	
	switch $OS$
		case "Windows_nt"
			DosInAnIcon_create_ini
		endcase
		case "Linux"
			ShellInAnIcon_create_ini
		endcase
		case "macos"
			ShellInAnIcon_create_ini
		endcase
	endswitch
	Patches_dummy_ini $HomeTestFiles$+"\dummy.ini"
	if (FileExists($HometestFiles$+"\dummy.ini"))
		message "add"
		set $ConstTest$ = "[secdummy]"
		set $ConstTest2$ = "dummy1=add1"
		Set $list1$ = loadTextFile($HomeTestFiles$+"\dummy.ini")
		Set $CompValue$ = Trim(takeString(0, $list1$))
		Set $CompValue2$ = Trim(takeString(1, $list1$))
		if (($ConstTest$ = $CompValue$) AND ($ConstTest2$ = $CompValue2$))
			comment "add passed"
		else
			set $TestResult$ = "not o.k."
			LogWarning "add failed"
		endif
		
		message "set"
		set $ConstTest$ = "[secdummy]"
		set $ConstTest2$ = "dummy2=set1"
		Set $list1$ = loadTextFile($HomeTestFiles$+"\dummy.ini")
		Set $CompValue$ = Trim(takeString(0, $list1$))
		Set $CompValue2$ = Trim(takeString(2, $list1$))
		if (($ConstTest$ = $CompValue$) AND ($ConstTest2$ = $CompValue2$))
			comment "set passed"
		else
			set $TestResult$ = "not o.k."
			LogWarning "set failed"
		endif
		
		message "addnew"
		set $ConstTest$ = "[secdummy]"
		set $ConstTest2$ = "dummy1=addnew1"
		Set $list1$ = loadTextFile($HomeTestFiles$+"\dummy.ini")
		Set $CompValue$ = Trim(takeString(0, $list1$))
		Set $CompValue2$ = Trim(takeString(6, $list1$))
		if (($ConstTest$ = $CompValue$) AND ($ConstTest2$ = $CompValue2$))
			comment "addnew passed"
		else
			set $TestResult$ = "not o.k."
			LogWarning "addnew failed"
		endif
		
		message "change"
		set $ConstTest$ = "[secdummy]"
		set $ConstTest2$ = "dummy3=change1"
		Set $list1$ = loadTextFile($HomeTestFiles$+"\dummy.ini")
		Set $CompValue$ = Trim(takeString(0, $list1$))
		Set $CompValue2$ = Trim(takeString(3, $list1$))
		if (($ConstTest$ = $CompValue$) AND ($ConstTest2$ = $CompValue2$))
			comment "change passed"
		else
			set $TestResult$ = "not o.k."
			LogWarning "change failed"
		endif
		
		message "delete"
		set $ConstTest$ = "[secdummy]"
		set $ConstTest2$ = "dummy5=add5"
		Set $list1$ = loadTextFile($HomeTestFiles$+"\dummy.ini")
		Set $CompValue$ = Trim(takeString(0, $list1$))
		Set $CompValue2$ = Trim(takeString(4, $list1$))
		if (($ConstTest$ = $CompValue$) AND ($ConstTest2$ = $CompValue2$))
			comment "delete passed"
		else
			set $TestResult$ = "not o.k."
			LogWarning "delete failed"
		endif
		
		message "replace"
		set $ConstTest$ = "[secdummy]"
		set $ConstTest2$ = "replace1=replace1"
		Set $list1$ = loadTextFile($HomeTestFiles$+"\dummy.ini")
		Set $CompValue$ = Trim(takeString(0, $list1$))
		Set $CompValue2$ = Trim(takeString(5, $list1$))
		if (($ConstTest$ = $CompValue$) AND ($ConstTest2$ = $CompValue2$))
			comment "replace passed"
		else
			set $TestResult$ = "not o.k."
			LogWarning "replace failed"
		endif
	else
		set $TestResult$ = "not o.k."
		logError "Fatal: could not create .ini file"
		isFatalError
	endif
	
	comment ""
	comment "-------------------------------------"
	comment "Testing: "
	message "GetValueFromInifile"
	if (FileExists($HometestFiles$+"\dummy.ini"))
		;GetValueFromInifile
		set $ConstTest$ = "add1"
		Set $CompValue$ = GetValueFromInifile($HometestFiles$+"\dummy.ini", "secdummy", "dummy1", "failed")
		if ($ConstTest$ = $CompValue$)
			comment "GetValueFromInifile passed"
		else
			set $TestResult$ = "not o.k."
			LogWarning "GetValueFromInifile failed"
		endif
		
		message "GetIni"
		comment "GetIni is deprecated - use GetValueFromInifile"
		set $ConstTest$ = "add1"
		Set $CompValue$ = GetIni($HometestFiles$+"\dummy.ini"[secdummy] dummy1)
		if ($ConstTest$ = $CompValue$)
			comment "GetIni passed"
		else
			set $TestResult$ = "not o.k."
			LogWarning "GetIni failed"
		endif
		
		comment "Here comes the actual ini file:"
		Set $list1$ = loadTextFile($HomeTestFiles$+"\dummy.ini")
	else
		set $TestResult$ = "not o.k."
		logError "Fatal: could not create .ini file"
		isFatalError
	endif
	
	
	comment ""
	comment "-------------------------------------"
	comment "Testing: "
	message "delsec"
	
	set $SectionName$ ="[secdummy]"
	Patches_dummy_ini_delsec $HomeTestFiles$+"\dummy.ini"
	if CompareDotSeparatedNumbers("%OpsiscriptVersion%",">=","4.12.1.0")
		; encoding in patches has to be fixed
		if (FileExists($HometestFiles$+"\dummy.ini"))
			set $ConstTest$ = "[secdüöä]"
			Set $list1$ = loadTextFile($HomeTestFiles$+"\dummy.ini")
			Set $CompValue$ = Trim(takeString(0, $list1$))
			if ($ConstTest$ = $CompValue$)
				comment "passed"
			else
				set $TestResult$ = "not o.k."
				LogWarning "failed"
			endif
		else
			set $TestResult$ = "not o.k."
			logError "Fatal: could not find .ini file"
			isFatalError
		endif
		set $SectionName$ ="[secdüöä]"
		Patches_dummy_ini_delsec $HomeTestFiles$+"\dummy.ini"
		if (FileExists($HometestFiles$+"\dummy.ini"))
			set $ConstTest$ = ""
			Set $list1$ = loadTextFile($HomeTestFiles$+"\dummy.ini")
			Set $CompValue$ = Trim(takeString(0, $list1$))
			if ($ConstTest$ = $CompValue$)
				comment "passed"
			else
				set $TestResult$ = "not o.k."
				LogWarning "failed"
			endif
		else
			set $TestResult$ = "not o.k."
			logError "Fatal: could not find .ini file"
			isFatalError
		endif
	endif ; 4.12.1.0
	
	Files_remove_folder
	
	if CompareDotSeparatedNumbers("%OpsiscriptVersion%","4.11.3.2") >= "0"
		comment ""
		comment "-------------------------------------"
		comment "Testing: "
		message "Patches /AllNTUserProfiles"
		Patches_dummy_ini "%userprofiledir%\opsi-winst-test.ini" /AllNTUserProfiles
		set $list1$ = getProfilesDirList
		for %s% in $list1$ do sub_check_profile_ini
		Files_remove_profile_ini /AllNTUserProfiles
	endif ; 4.11.3.2
	
	if CompareDotSeparatedNumbers("%OpsiscriptVersion%",">=","4.12.5.0")
		comment ""
		comment "-------------------------------------"
		comment "Testing: "
		message "Patches /AllUserProfiles with userProfileDir constant"
		Patches_userprofiledir "%userprofiledir%\opsi-winst-test.ini" /AllUserProfiles
		set $list1$ = getProfilesDirList
		for %s% in $list1$ do sub_check_profile_dir
		Files_remove_profile_ini /AllNTUserProfiles
	endif ; 4.12.5.0
	
	switch $OS$
		case "Windows_nt"
			DosInAnIcon_create_ini
		endcase
		case "Linux"
			ShellInAnIcon_create_ini
		endcase
		case "macos"
			ShellInAnIcon_create_ini
		endcase
	endswitch
	
	Patches_dummy_ini $HomeTestFiles$+"\dummy.ini"
	if CompareDotSeparatedNumbers("%OpsiscriptVersion%",">=","4.12.4.13")
		;if $test_experimental$ = "true"
		
		comment ""
		comment "-------------------------------------"
		comment "Testing: "
		message "GetSectionFromInifile"
		;GetSectionFromInifile
		
		if (FileExists($HometestFiles$+"\dummy.ini"))
			
			set $list1$ = createStringList ('dummy1=add1', 'dummy2=set1', 'dummy3=change1', 'dummy5=add5', 'replace1=replace1', 'dummy1=addnew1')
			Set $CompList1$ = GetSectionFromInifile("secdummy", $HometestFiles$+"\dummy.ini")
			if areListsEqual($list1$,$CompList1$,"FLAG_AUTO_CS")
				comment "GetSectionFromInifile passed"
			else
				set $TestResult$ = "not o.k."
				LogWarning "GetSectionFromInifile failed"
			endif
			
			set $list2$ = createStringList ('dümmy7=üöäßÜÖÄ')
			Set $CompList2$ = GetSectionFromInifile("secdüöä", $HometestFiles$+"\dummy.ini")
			if areListsEqual($list2$,$CompList2$,"FLAG_AUTO_CS")
				comment "GetSectionFromInifile passed"
			else
				set $TestResult$ = "not o.k."
				LogWarning "GetSectionFromInifile failed"
			endif
			
			;set $test1list1$ = getSubListByMatch($list1$,$CompList1$)
			;set $test2list1$ = getSubListByMatch($CompList1$,$list1$)
			;
			;set $test1list2$ = getSubListByMatch($list2$,$CompList2$)
			;set $test2list2$ = getSubListByMatch($CompList2$,$list2$)
			;
			;if (count($test1list1$)=count($list1$) AND count($test2list1$)=count($CompList1$) AND count($test1list2$)=count($list2$) AND count($test2list2$)=count($CompList2$))
			;	comment "GetSectionFromInifile passed"
			;else
			;	set $TestResult$ = "not o.k."
			;	LogWarning "GetSectionFromInifile failed"
			;endif
		else
			set $TestResult$ = "not o.k."
			logError "Fatal: could not create .ini file"
			isFatalError
		endif
	endif ; 4.12.4.13
	
	Files_empty_folder ;remove test files
	
	sub_compareResult
	set $winst_patches$ = $SubResult$
endif

[DosInAnIcon_create_ini]
c:
mkdir $HomeTestFiles$
cd $HomeTestFiles$
del dummy.ini

[ShellInAnIcon_create_ini]
set -x
mkdir -p "$HomeTestFiles$"
cd "$HomeTestFiles$"
rm dummy.ini

[Patches_dummy_ini]
add [secdummy] dummy1=add1
; will be overwritten by other functions
add [secdummy] dummy2=add2
add [secdummy] dummy3=add3
add [secdummy] dummy4=add4
add [secdummy] dummy5=add5
add [secdummy] dummy6=add6
add [secdüöä] dümmy7=üöäßÜÖÄ

set [secdummy] dummy2=set1
addnew [secdummy] dummy1=addnew1
change [secdummy] dummy3=change1
del [secdummy] dummy4
Replace dummy6=add6 replace1=replace1

[Patches_dummy_ini_delsec]
delsec $SectionName$

[Files_remove_folder]
Delete -s -f $HomeTestFiles$

[Files_remove_profile_ini]
del "%userprofiledir%\opsi-winst-test.ini"

[sub_check_profile_ini]
set $ConstTest$ = "add1"
Set $CompValue$ = GetValueFromInifile("%s%\opsi-winst-test.ini", "secdummy", "dummy1", "failed")
if ($ConstTest$ = $CompValue$)
	comment "GetValueFromInifile passed"
else
	set $TestResult$ = "not o.k."
	LogWarning "Patches /AllNTUserProfiles"
endif


[Patches_userprofiledir]
set [secdummy] userprofile=%userProfileDir%

[sub_check_profile_dir]
set $ConstTest$ = "%s%"
Set $CompValue$ = GetValueFromInifile("%s%\opsi-winst-test.ini", "secdummy", "userprofile", "failed")
if ($ConstTest$ = $CompValue$)
	comment "GetValueFromInifile passed"
else
	set $TestResult$ = "not o.k."
	LogWarning "Patches /AllNTUserProfiles"
endif
