[Actions]
comment "Testing: "
message "GetSectionFromInifile"

DefVar $TestResult$
DefVar $OS$
DefVar $HomeTestFiles$
DefStringList $list1$
DefStringList $list2$

DefFunc myGetSectionFromInifileTester($file$ : string, $encoding$: string, ref $TestResult$ : string) : void
	DefStringList $CompValue1$
	DefStringList $CompValue2$
	
	set $list1$ = createStringList ('dummy1=add1', 'dummy2=set1', 'dummy3=change1', 'dummy5=add5', 'replace1=replace1', 'dummy1=addnew1')
	set $list2$ = createStringList ('dümmy7=üöäßÜÖÄ')

	if (FileExists($file$))
		if ($encoding$= "")
			Set $CompValue1$ = GetSectionFromInifile("secdummy", $file$)
			Set $CompValue2$ = GetSectionFromInifile("secdüöä", $file$)
		else
			if (lower($encoding$)= "ascii")
				Set $CompValue1$ = GetSectionFromInifile("secdummy", $file$)
				Set $CompValue2$ = GetSectionFromInifile("secduoa", $file$)
				Set $list2$ = createStringList ('dummy7=uoasUOA')
			else
				Set $CompValue1$ = GetSectionFromInifile("secdummy", $file$, $encoding$)
				Set $CompValue2$ = GetSectionFromInifile("secdüöä", $file$, $encoding$)
			endif
		endif
		
		if (areListsEqual($list1$,$CompValue1$,"FLAG_AUTO_CS") AND areListsEqual($list2$,$CompValue2$,"FLAG_AUTO_CS"))
			comment "GetSectionFromInifile passed"
		else
			set $TestResult$ = "not o.k."
			LogWarning "GetSectionFromInifile failed"
		endif
	else
		set $TestResult$ = "not o.k."
		logError "Fatal: ini file does not exist"
		isFatalError
	endif  
endfunc

; This test needs opsi-script-test.opsiscript to be run first
; That will create the $HomeTestFiles$ file

Set $OS$ = GetOS

switch $OS$
	case "Windows_nt"
		comment "We are running on Windows"
		Set $HomeTestFiles$ = "%opsiTmpDir%\testFiles\"
	endcase
	case "Linux"
		comment "We are running on Linux"
		Set $HomeTestFiles$ = "/tmp/opsi-script-test/testFiles/"
	endcase
	case "macos"
		comment "We are running on macOS"
		Set $HomeTestFiles$ = "/tmp/opsi-script-test/testFiles/"
	endcase
endswitch

Set $HomeTestFiles$ = "/home/jinene/gitwork/opsi-script-test/CLIENT_DATA/test-files/ini/"


myGetSectionFromInifileTester($HometestFiles$+"dummy.ini","",$TestResult$)
myGetSectionFromInifileTester($HometestFiles$+"dummy.ini", "default",$TestResult$)
myGetSectionFromInifileTester($HometestFiles$+"dummy_ascii.ini", "ASCII",$TestResult$)
myGetSectionFromInifileTester($HometestFiles$+"dummy_ansi.ini", "ANSI",$TestResult$)
myGetSectionFromInifileTester($HometestFiles$+"dummy_utf8.ini", "UTF8",$TestResult$)
myGetSectionFromInifileTester($HometestFiles$+"dummy_utf16.ini", "UTF16",$TestResult$)
myGetSectionFromInifileTester($HometestFiles$+"dummy_utf16be.ini", "UTF16BE",$TestResult$)
