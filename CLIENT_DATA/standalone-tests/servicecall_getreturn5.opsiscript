[actions]

importlib "servicecall_auth_lib.opsiscript"

authenticate()

DefVar $myfqdn$
set $myfqdn$ = "win10x64.vmnat.local"

DefStringList $list1$
DefVar $installationStatus$
DefVar $objstr$
DefVar $arraystr$

set $list1$ = getReturnListFromSection('opsiservicecall_myid')
set $arraystr$ = takestring(0,$list1$)

if not (jsonIsValid($arraystr$))
  logError "Not a valid json string"
else
  if not (jsonIsArray($arraystr$))
    logError "Not a valid json array"
  else
    comment "take the first object from array"
    set $objstr$ = jsonAsArrayGetElementByIndex($arraystr$,"0")
    comment "Delete ident"
    set $objstr$ = jsonAsObjectDeleteByKey($objstr$,"ident")
    comment "set installationstatus"
    set $objstr$ = jsonAsObjectSetStringtypeValueByKey($objstr$,"installationStatus","installed")
    comment "write the first object back to array"
    set $arraystr$ = jsonAsArrayPutObjectByIndex($arraystr$,"0",$objstr$)
    comment "write array back"
    opsiservicecall_writeback
  endif
endif

[opsiservicecall_myid]
"method":"productOnClient_getObjects"
"params":[
          "[]"
          '{"clientId":"$myfqdn$","productId":"javavm"}'
         ]

[opsiservicecall_writeback]
"method":"productOnClient_createObjects"
"params":[
          '$arraystr$'
         ]